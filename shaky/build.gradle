apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

allprojects {
    group = "com.linkedin.shaky-android"
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
    }
}

dependencies {
    compile 'com.squareup:seismic:1.0.1'
    compile "com.android.support:appcompat-v7:$supportLibrariesVersion"
    compile "com.android.support:recyclerview-v7:$supportLibrariesVersion"
    compile "com.android.support:support-v4:$supportLibrariesVersion"
    compile "com.android.support:support-annotations:$supportLibrariesVersion"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.robolectric:robolectric:3.0'
}

version = VERSION_NAME

publishing {
    publications {
        aar(MavenPublication) {
            groupId GROUP_ID

            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar") {
                builtBy assembleRelease
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    description = LIBRARY_DESCRIPTION
                    artifactId = ARTIFACT
                    url 'https://github.com/linkedin/shaky-android'
                    scm {
                        url 'https://github.com/linkedin/shaky-android'
                        connection 'scm:git:git://github.com/linkedin/shaky-android.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'com.linkedin'
                            name 'LinkedIn Corp'
                            email ''
                        }
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://linkedin.jfrog.io/linkedin'
    publish {
        repoKey = 'shaky-android'
        username = System.getenv('ARTIFACTORY_USER') ?: ''
        password = System.getenv('ARTIFACTORY_KEY') ?: ''

        defaults {
            publications ('aar')
            publishArtifacts = true
        }
    }
    clientConfig.setIncludeEnvVars(false)
}